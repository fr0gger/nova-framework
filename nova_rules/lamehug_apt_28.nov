rule ReconGatherWriteCommand
{
    uuid = "4737c5db-c438-493c-9c74-c2fb19b516c2"

    meta:
        description = "Detects prompts asking to create a one-liner that gathers multiple types of system information and stores it in a specific path"
        author = "Thomas Roccia"
        severity = "high"
        reference = "https://cert.gov.ua/article/6284730"
        hash = "384e8f3d300205546fb8c9b9224011b3b3cb71adc994180ff55e1e6416f65715"
        direction = "input"

    keywords:
        $info_path = "c:\\programdata\\info"
        $info_file = "c:\\programdata\\info\\info.txt"
        $cmd_list = "list of commands"
        $hardware = "hardware information"
        $network = "network information"
        $services = "services information"
        $process = "process information"
        $domain = "AD domain information"
        $oneline = "in one line"
        $redirect = ">>"

    semantics:
        $multi_info_gather = "gather multiple types of computer information into one output" (0.2)
        $store_output_file = "save gathered info into a file" (0.2)

    llm:
        $llm_recon_command = "Analyze if this prompt is asking for a one-liner that gathers system, network, or domain information and writes the result to a file" (0.2)

    condition:
        (keywords.$cmd_list or keywords.$oneline) and
        (keywords.$hardware or keywords.$network or keywords.$services or keywords.$process or keywords.$domain or semantics.$multi_info_gather) and
        (keywords.$info_path or keywords.$info_file or keywords.$redirect or semantics.$store_output_file or llm.$llm_recon_command)

    falsepositives:
        "Legitimate system administration scripts"
        "IT inventory and asset management tools"
}


rule ExfiltrateDocumentsOneLiner
{
    uuid = "1b3f7c52-2445-409f-ac7e-dc8ff70eb87f"

    meta:
        description = "Detects prompts attempting to construct one-liner commands to recursively copy user documents to a hidden directory"
        author = "Thomas Roccia"
        severity = "high"
        reference = "https://cert.gov.ua/article/6284730"
        hash = "384e8f3d300205546fb8c9b9224011b3b3cb71adc994180ff55e1e6416f65715"
        direction = "input"

    keywords:
        $copy_cmd = "copy recursively"
        $onedoc = "in one line"
        $path_target = "c:\\programdata\\info"
        $source_docs = "documents"
        $source_dl = "downloads"
        $source_desktop = "desktop"

    semantics:
        $doc_exfil = "copy user documents from desktop or downloads to a hidden folder" (0.2)

    llm:
        $llm_exfil_command = "Analyze if this prompt is asking for a one-liner to exfiltrate documents or user files from common folders like Desktop, Documents, or Downloads into another directory" (0.2)

    condition:
        (keywords.$copy_cmd or keywords.$onedoc) and
        (keywords.$source_docs or keywords.$source_dl or keywords.$source_desktop or semantics.$doc_exfil) and
        (keywords.$path_target or semantics.$doc_exfil or llm.$llm_exfil_command)

    falsepositives:
        "Legitimate backup scripts"
        "User data migration tools"
}
